plugins {
    id 'com.github.johnrengelman.shadow'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"
    implementation project(path: ':common_config')
    shadowBundle project(path: ':common_config')
    shadowBundle 'com.electronwill.night-config:toml:3.6.4'
    shadowBundle 'com.electronwill.night-config:json:3.6.4'
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    archiveClassifier.set('dev-shadow')
    configurations = [project.configurations.shadowBundle]
    relocate 'com.electronwill.nightconfig', 'dev.kostromdan.mods.crash_assistant.nightconfig'
}

remapJar {
    dependsOn(':app:shadowJar', ':forge:build')

    from(project(':app').tasks.shadowJar) {
        into 'META-INF/jarjar'
    }
    from(project(':forge').tasks.remapJar) {
        rename { String fileName ->
            "crash_assistant-forge.jar"
        }
        into 'META-INF/jarjar'
    }
    from(zipTree(project(':forge').tasks.remapJar.archiveFile.get().asFile)) {
        include 'META-INF/mods.toml'
        into ''
    }

    input.set shadowJar.archiveFile
}

unifiedPublishing {
    project {
        gameVersions = ["1.19.2", "1.19.3", "1.19.4", "1.20.1"]
        gameLoaders = ["forge", "neoforge"]

        displayName = tasks.remapJar.archiveFile.get().asFile.name
        version = mod_version
        changelog = file('../changelog.md').text
        releaseType = release_type

        mainPublication tasks.remapJar

        var cfToken = System.getenv("CF_TOKEN")
        if (cfToken != null) {
            curseforge {
                token = cfToken
                id = "1154099"
            }
        }

        var mrToken = System.getenv("MODRINTH_TOKEN")
        if (mrToken != null) {
            modrinth {
                token = mrToken
                id = "ix1qq8Ux"
            }
        }
    }
}